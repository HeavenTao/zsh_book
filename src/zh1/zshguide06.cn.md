------------------------------------------------------------------------

-   [目录](zshguide.html)
-   [上一章](zshguide05.html)
-   [下一章](zshguide07.html)

------------------------------------------------------------------------

[]{#comp}[]{#l144}

# 第六章：补全，旧与新

命令参数的补全是 zsh 特别擅长的一件事。最简单的情况是，你按 `<TAB>`，shell 会猜测那里应该是什么并为你填充：

      % ls
      myfile  theirfile  yourfile
      % cat t<TAB>

将命令行展开为

      % cat theirfile

你只需要输入首字母，然后按 `TAB`。

在 C shell 中这个功能刚出现的时候，只能补全文件名；如果名称有歧义，没有任何聪明的技巧可以帮助你，它只是打印出无歧义的部分然后发出哔哔声，让你自己决定下一步该做什么。你也可以列出可能的补全；出于某种原因，这在 csh 中被附加到了 `^D` 键上，在后来的具有类 Emacs 绑定的 shell 中，这个键也删除下一个字符，所以历史赋予了 zsh，像其他 shell 一样，有点奇怪的组合行为：

      % cat yx 

现在将光标移回一个字符到 x 上，然后按两次 ^D，你会看到：`yourfile`。如果你使用类 vi 的绑定，或者，显然，如果你重新绑定了 `^D`，这就行不通了。

接下来，补全其他项目如用户名、命令或主机名成为可能。然后 zsh 介入了菜单补全，所以你可以一直盲目地按 `<TAB>` 直到正确的答案出现，而永远不需要自己输入一个额外的字符。

下一个发展是 tcsh 的，然后是 zsh 的，可编程补全系统；你可以给 shell 指令，在某些上下文，只应该补全某些项目；例如，在 `cd` 之后，你只会想要目录。在 tcsh 中，有一个名为 `complete` 的命令；每个 `complete ...` 语句定义了特定命令（如 `cd`）参数的补全；在 zsh 中等价的是 `compctl`，它受到 `complete` 的启发，但在几乎所有重要细节上都不同。在 shell 发行版的 `Misc` 目录中有一个 perl 脚本 `lete2ctl` 可以帮助你从 tcsh 格式转换为 zsh 格式。你把一整套 `compctl` 命令放到 `.zshrc` 中，其他一切都由 shell 完成。

Zsh 的系统变得越来越复杂，在 3.1.6 版本中出现了一个新的补全系统，它应该为你做所有事情：你只需从一个初始化文件中调用一个函数 `compinit`，之后 zsh 就知道，例如，`gunzip` 后面应该跟着以 `.gz` 结尾的文件。新系统基于 shell 函数，这是一个额外的奖励，因为它们非常灵活，而且你已经知道语法了。然而，考虑到其复杂性，现在开始编写自己的补全相当困难，而且要弄清楚如何按照你喜欢的方式更改设置也足够难了。本章的其余部分应该会有所帮助。

我将专注于新的补全系统，它似乎最终将完全取代旧的系统，现在 3.1 发布系列已经成为 4.0 产品发布。旧的 **compctl** 命令仍然可用，旧的补全定义在未来的 zsh 版本中将继续工作——事实上，在大多数支持动态链接库的操作系统上，旧的补全系统在一个不同的文件中，shell 在必要时会加载它，所以这几乎没有开销。

新系统的一大区别是，不是在 shell 启动时一次性设置好所有东西，而是在你按 `<TAB>` 之后调用各种 shell 代码片段，以便当场生成补全。shell 中有足够多的新东西，所有那些 `compctl` 的难记选项（`-f` 用于文件，`-v` 用于变量等等）都可以被直接产生补全列表的命令所取代；在这种情况下，关键命令叫做 `compadd`，它被传递这个列表并决定用什么来补全命令行上的单词。所以最简单形式的新补全看起来大致是这样的：

      # 告诉 shell 函数 mycompletion 可以在被小部件名称 my-completion-widget 调用时进行补全，
      # 并且它的行为像现有的小部件 complete-word
      zle -C my-completion-widget .complete-word mycompletion
      
      # 定义一个调用补全小部件的键
      bindkey '^x^i' my-completion-widget
      
      # 定义将被调用的函数
      mycompletion() {
        # 添加一个补全列表
        compadd alpha bravo charlie delta
      }

这大致就是补全系统正在做的事情，除了函数被称为 `_main_complete` 并且它会根据调用补全的上下文（所有 `compctl` 过去做的事情）调用许多其他函数来做它的脏活，而小部件只是旧的补全小部件（`expand-or-complete` 等）被重新定义并仍然绑定到所有原始键。但是，如果你还没猜到，事情远不止于此。

这是本章各节的计划。

1.  对补全和展开的广泛描述，同样适用于新旧补全。
2.  如何使用 shell 选项配置补全。本节的大部分内容也适用于旧补全，尽管我不会明确标记任何差异。在此之后，我将把 `compctl` 的世界抛在脑后。
3.  如何启动新的补全。
4.  新补全系统如何工作的基本原理。
5.  如何使用新的 `zstyle` 内建命令来配置它。
6.  除了通常的补全系统之外，还有一些做其他事情的独立命令，以及一些与补全有关的其他编辑小部件。
7.  匹配控制，一种强大的方式来决定诸如是否不区分大小写地补全，是否允许在标点符号前插入单词的额外部分，或者是否忽略命令行上单词中的某些字符。
8.  如何编写自己的补全函数；你不需要对所有前面的内容有太扎实的理解就能做简单的补全，但我会逐渐介绍如何让标签和样式在你自己的函数中工作的全部巴洛克式的辉煌，以及如何让补全完成处理命令参数和选项的工作。
9.  按照旧的“开头、中间、结尾”原则，优雅地结束本章。

[]{#l145}

## 6.1: 补全和展开

当你按 tab 键时，发生的事情不止是补全。zsh 尝试做的第一件事是展开行。展开在上一章已经讲过：基本上那里描述的所有东西都是编辑器内联展开的可能候选项。换句话说，带感叹号的历史替换、使用 `$` 或反引号的各种展开，以及文件名生成（通配）都可以发生，结果会替换命令行上的内容：

      % echo $PWD<TAB>
        ->    echo /home/pws/zsh/projects/zshguide
      % echo `print $ZSH_VERSION`<TAB>
        ->    echo 3.1.7
      % echo !!<TAB>
        ->    echo echo 3.1.7
      % echo ~/.z*<TAB>
        ->    echo /home/pws/.zcompdump /home/pws/.zlogout
              /home/pws/.zshenv /home/pws/.zshrc                   

请注意，在这种情况下，`~` 也会被展开。

这通常是记住“撤销”键 `^_` 或 `^Xu` 的好时机；如果你不喜欢结果，输入这个将恢复展开前的内容。许多键盘有一个怪癖，被描述为 `^_` 的东西应该通过按住 control 和斜杠来输入，你会写成 `^/`，但不幸的是那会做别的事情；这不是 zsh 的错。还有一个半例外，即文件名生成：像 `~/file` 这样的路径不会被展开，因为你通常知道它们指的是什么，而且为了补全而保留它们通常很方便。然而，`=cmdname` 形式确实会被展开，除非你设置了 `NO_EQUALS`。

实际上，决定是进行展开还是补全有时可能很棘手，因为如果它们是完整的，会被展开的东西，可能需要先被补全；例如 `$PAT` 可能应该被补全为 `$PATH`，但很可能也有一个参数 `$PAT`。如果你愿意，你可以决定是哪个。首先，命令 `expand-word`，绑定到 `^X*`，以及相应的用于列出将被展开的内容的命令 `list-expand`，绑定到 `^Xg`，只做展开——所有可能的形式，除了别名展开，包括将 `~/file` 变成一个完整路径。

从另一个角度看，你可以使用除了 `expand-or-complete`（默认绑定到 `<TAB>` 的那个）之外的命令来只执行补全。这样做的基本命令是 `complete-word`，默认没有绑定。如果你乐于使用单独的命令进行展开，将它绑定到 `^I`（即 `<TAB>`）是相当明智的，即

      # 现在 tab 只做补全，不做展开
      bindkey '^i' complete-word

此外，如果你这样做并使用新的补全系统，那么正如我们将看到的，有一种方法可以让补全系统执行展开——请参阅下面对 `_expand` 补全器的描述。在这种情况下，你对尝试哪些展开形式以及在哪个点尝试有更多的控制，但你必须确保你使用 `complete-word`，而不是 `expand-or-complete`，否则标准的展开系统将接管。

`expand-or-complete` 有一个近亲，`expand-or-complete-prefix`，默认没有绑定。唯一的区别是，它在补全时会忽略光标下和右边的所有内容。就好像光标所在的位置有一个空格，所有要忽略的东西都向右移动了（猜猜它是如何实现的）。如果你习惯于在其他单词之前在行中输入新单词，并期望它们在输入空格之前就能自己补全或展开，就使用这个。其他一些 shell 一直都是这样工作的。更明确地说：

      % ls
      filename1
      % ls filex

将光标移动到 `x` 上并按 tab。使用 `expand-or-complete` 什么也不会发生；它试图补全一个名为 `filex` 的文件——或者，设置了 `COMPLETE_IN_WORD` 选项后，它试图找到一个以 `file` 开头并以 `x` 结尾的文件。如果你这样做

      bindkey '^i' expand-or-complete-prefix

并尝试同样的实验，你会发现整个东西被补全为 `filename1x`，所以 `x` 被忽略了，但没有被移除。

一个可能的陷阱是，列表命令，包括 `delete-char-or-list`（在 emacs 模式下默认绑定到 `^D`）和 `list-options`（在 vi 插入模式下默认绑定到 `^D`，并且是列出补全的基本命令，因为它没有删除字符的行为），不显示可能的展开，所以使用默认绑定，你可以用 `^D` 来列出，然后按 `<TAB>`，发现该行已被某个展开完全重写。使用 `complete-word` 而不是 `expand-or-complete` 当然可以解决这个问题。如果你知道如何编写新的编辑器小部件（[第四章](zshguide04.html#zle)），你可以编写一个函数，先尝试 `list-expand`，如果失败再尝试 `list-options`。

还有四个我还没提到的补全命令：三个是 `menu-complete`、`menu-expand-or-complete` 和 `reverse-menu-complete`，它们执行菜单补全，你可以通过按同一个键来循环浏览所有可能的补全。前两个分别对应于 `complete-word` 和 `expand-or-complete`，而第三个没有真正的等价物，因为它带你向后浏览一个补全列表。第三个的效果不能仅仅通过为菜单补全设置选项来达到，所以将它单独绑定是很有用的。我把它绑定到 `\M-\C-i`，即按住 Meta 键的 tab，但它默认没有绑定。

第四个是 `menu-select`，它执行一种增强形式的菜单补全，称为“菜单选择”，我将在下面谈论选项时描述。你需要确保加载了 `zsh/complist` 模块才能使用这个 zle 命令。如果你使用样式，zsh 应该能够在需要时自动加载这个，只要你有动态加载，你现在可能有了。 []{#l146}

## 6.2: 使用 shell 选项配置补全

在不编写或重写 shell 函数的情况下，改变补全行为主要有两种方法：shell 选项，如[第二章](zshguide02.html#init)中介绍的，和样式，如上所述。我将首先讨论 shell 选项，尽管如你所见，其中一些会引用样式机制。设置 shell 选项会影响每一个补全，除非采取了特殊措施（使用相应上下文的样式，或局部设置选项）来避免这种情况。

除了直接影响补全系统的选项外，补全还对描述 shell 行为的各种其他选项敏感。例如，如果设置了 `MAGIC_EQUAL_SUBST` 选项，那么所有看起来像 `foo=~/file` 的命令参数都会像在参数开头一样展开 `~`，那么对于未特殊处理的命令参数的默认补全将尝试在 `=` 之后补全文件名。

不用说，如果你编写补全函数，你需要担心很多其他可能影响 shell 语法的选项。由上下文选择的补全（除了绑定到按键的特定补全命令之外的所有内容）的主要起点是函数 `_main_complete`，它包含了以下行的效果，以确保至少在补全函数中设置了基本选项：

      setopt glob bareglobqual nullglob rcexpandparam extendedglob unset
      unsetopt markdirs globsubst shwordsplit shglob ksharrays cshnullglob
      unsetopt allexport aliases errexit octalzeroes

但这绝不是全部的可能性。实际上，它不包含那些行：要设置的选项存储在数组 `$_comp_options` 中，如果它们要被关闭，则前面有 `NO_`。如果你发现你需要修改这个，你可以修改它（也许也告诉维护者）。

顺便说一句，如果你想知道是否可以重用函数 `_main_complete`，通过将其绑定到具有稍微不同补全定义的不同键，请查看下面对 `_generic` 命令小部件的描述。它只是 `_main_complete` 的一个前端，允许你有一套不同的有效样式。

[]{#l147}

### 6.2.1: 模糊补全

最大的一组选项处理当补全是模糊的，换句话说，有多个可能的补全时会发生什么。七个相关的选项如下，从 FAQ 中复制而来；许多不同的组合是可能的：

-   设置 `NO_BEEP` 后，那烦人的哔哔声就消失了，
-   设置 `NO_LIST_BEEP` 后，哔哔声只对模糊补全关闭，
-   设置 `AUTO_LIST` 后，当补全模糊时，你会得到一个列表，而无需输入 `^D`，
-   设置 `BASH_AUTO_LIST` 后，列表只在你第二次对一个模糊补全按 tab 时发生，
-   设置 `LIST_AMBIGUOUS` 后，这被修改为如果有一个无歧义的前缀或后缀要插入，则不列出任何内容——这可以与 `BASH_AUTO_LIST` 结合使用，所以在两者都适用的情况下，你需要按三次 tab 才能列出，
-   设置 `REC_EXACT` 后，如果命令行上的字符串与可能的补全之一完全匹配，它就会被接受，即使还有另一个补全（即该字符串加上其他东西）也匹配，
-   设置 `MENU_COMPLETE` 后，一个补全总是被完全插入，然后当你按 TAB 时，它会变成下一个，依此类推，直到你回到你开始的地方，
-   设置 `AUTO_MENU` 后，你只有在对模糊补全再次按 TAB 时才会得到菜单行为。

[]{#l148}

### 6.2.2: `ALWAYS_LAST_PROMPT`

`ALWAYS_LAST_PROMPT` 选项默认是打开的，并且自 zsh 的一个早期 3.1 版本以来就是如此；在列出补全后，光标会回到它之前的行上，而不是在下面重新打印它。这样做的缺点是，当你执行命令或产生一个不同的列表时，列表会被遮挡，所以你可能想取消设置该选项。`ALWAYS_LAST_PROMPT` 行为是菜单选择工作所必需的，这就是为什么我现在提到它而不是在下面的杂项中。

当你编写自己的调用补全的编辑器函数时，你实际上可以用小部件 `end-of-list` 来取消这个效果，你会调用它为 `zle end-of-list`（它是一个普通的编辑函数，不是一个补全函数）。你也可以将它绑定到一个键上，用来保留现有的补全列表。另一方面，如果你想在补全函数内部控制行为，即决定补全是否会尝试返回到列表上方的提示符，你可以用 `$compstate` 关联数组的 `last_prompt` 元素来操作它，所以例如：

      compstate[last_prompt]=''

将为正在进行的补全关闭该行为。如果你发现自己想要如此详细地控制补全行为，`$compstate` 是你应该求助的地方；请参阅 `zshcompwid` 手册页。

[]{#l149}

### 6.2.3: 菜单补全和菜单选择

由上述选项决定的最重要的事情是你是否正在使用菜单补全。如果你不是，当补全有歧义时，你需要明确地输入下一个字符；如果你是，你只需要一直按 tab 直到你想要的补全出现。在第二种情况下，当然，如果可能性不多，这效果最好。使用 `AUTO_MENU` 或将 `menu-complete` 小部件绑定到一个单独的按键，可以让你两全其美。

在 3.1.6 中出现了一种新的菜单补全变体；事实上，它比原始形式更配得上菜单补全这个名字，但由于那个名字已经被占用了，它被称为“菜单选择”。这允许你在补全列表中移动光标来选择一个。它由一个单独的模块 `zsh/complist` 实现；你可以通过在 `.zshrc` 中放入 `zmodload -i zsh/complist` 来确保这个模块被加载，尽管如果你使用下面设置的 `menu` 样式，它应该会自动加载。为了让它有用，你需要另外两样东西。第一是 `ALWAYS_LAST_PROMPT` 行为；如果整个补全列表无法在屏幕上显示，这个行为会被抑制，因为屏幕上没有可以返回的行。然而，菜单选择仍然有效，它允许你上下滚动列表。第二件事是，你需要以任何通常的方式启动菜单补全；菜单选择是菜单补全的补充，而不是替代。

现在你应该设置以下样式：

      zstyle ':completion:*' menu select=<NUM>

如果一个模糊的补全产生了至少 `<NUM>` 个可能性，菜单选择就会启动。你最好通过尝试来理解这个。列表中的一个补全，最初是左上角的那个，被高亮显示并插入到行中。通过在明显的方向上移动光标（在边缘处有环绕），你可以改变高亮显示的值和插入到行中的值。当你得到你想要的值时，按回车，这会移除列表并留下插入的值。按 `^G`（编辑器函数 `send-break`）会中止菜单选择，移除列表并恢复命令行。

在内部，zsh 实际上使用参数 `$MENUSELECT` 来提供数字，从而启动菜单选择。然而，这总是从上面定义的样式中初始化的，所以你不应该直接设置 `$MENUSELECT`（除非你正在使用 `compctl`，它会愉快地使用菜单选择）。与其他样式一样，你可以为不同的上下文指定不同的值；如果当前上下文没有为当前标签的样式产生一个值，则会检查 `default` 标签。请注意，`menu` 样式也允许你控制是否启动菜单补全，无论是否带选择；换句话说，它是一个对应于 `MENU_COMPLETE` 选项的样式。

使用菜单选择时还有一个额外的功能。zle 命令 `accept-and-infer-next-history` 在这里有不同的含义；它接受一个补全，然后尝试再次使用菜单选择进行补全。这对于目录层次结构非常有用，并且与 `undo` 结合使用，可以给你一个简单的文件浏览器。你需要在特殊的键映射 `menuselect` 中绑定它；例如，我使用

      bindkey -M menuselect '^o' accept-and-infer-next-history

因为这种行为让我想起了在 emacs 模式下通常绑定到 `^O` 的东西，即 `accept-line-and-down-history`。这样绑定对普通键映射中的 `^O` 没有影响。通过在一个包含目录的文件集上进入菜单选择，并在其中一个目录上输入 `^O` 来尝试它。你应该会立即看到该目录的内容被呈现出来以供下一次选择，而 `undo` 足够聪明，不仅能移除那个选择，还能返回到对父目录的补全。

你可以选择补全列表中当前选定值的突出显示方式，使用的机制与为特定类型的匹配指定颜色的机制完全相同；请参阅下面对 `list-colors` 样式的描述。

[]{#l149}

### 6.2.4: 改变补全行为的其他方式

**`COMPLETE_ALIASES`**

如果你设置了一个别名，比如

      alias pu=pushd

那么当补全系统寻找命令名称时，别名 `pu` 将被展开，所以它会找到命令名称 `pushd`。这对于避免为所有别名定义额外的补全非常有用。然而，你可能想为别名定义一些与它展开的命令不同的东西。在这种情况下，你需要设置 `COMPLETE_ALIASES`，并为每个不匹配命令名称的别名安排补全。因此，`alias zcat="myzcat -dc"` 在设置了该选项的情况下会起作用，即使你没有告诉系统关于 `myzcat` 的信息，而 `alias myzcat="gzip -dc"` 除非你为 myzcat 定义一个补全，否则不会起作用：这里 `compdef _gzip myzcat` 可能就足够了。没有设置该选项时，情况会相反：第一个别名没有额外的 `compdef` 就不会起作用，但第二个会。

**`AUTO_REMOVE_SLASH`**

这个选项默认是打开的。如果你补全一个目录名并且添加了一个斜杠——通常是这样，既告诉你你补全了一个目录，也允许你在不手动添加 `/` 的情况下补全其中的文件——而你输入的下一个东西*不是*会插入或补全该目录中文件的一部分的东西，那么斜杠就会被移除。因此：

     % rmdir my<TAB>
       ->  rmdir mydir/
     % rmdir mydir/<RETURN>
       ->  `rmdir mydir' executed
     

这个例子显示了为什么添加这个行为：一些版本的 `rmdir` 会对目录名后的斜杠感到不满。另一方面，如果你在斜杠后继续输入，或者再次按 tab 在 `mydir` 内部补全，那么斜杠会保留。

在大多数情况下，这最多是无害的。然而，如果你不喜欢这种行为，你可以取消设置 `AUTO_REMOVE_SLASH` 选项。一个可能引起轻微混淆的事情是，虽然它与其他后缀（即自动添加但不是被补全的值的一部分的东西）相同，但如果值是通过菜单补全插入的，斜杠会立即被添加。这可能会让你错误地认为补全已经完成，因此是唯一的，而实际上并非如此。

请注意，某些形式的补全内置了这种行为，不一定带斜杠，当补全参数列表时。例如，输入 `typeset ZSH_V<TAB>`，你会看到 `ZSH_VERSION=` 出现，以防你想给该参数赋一些值；按空格，这不是一个可能的值，会使 `=` 消失。这不受 `AUTO_REMOVE_SLASH` 选项的控制，该选项只适用于由标准文件名补全系统插入的目录。

**`AUTO_PARAM_SLASH`, `AUTO_PARAM_KEYS`**

这些选项在补全带参数替换的表达式时生效。如果设置了 `AUTO_PARAM_SLASH`，那么任何其值为目录名的参数表达式在补全时都会附加一个斜杠，就像值本身被补全系统插入一样。

`AUTO_PARAM_KEYS` 的行为有点复杂。试试这个：

      print ${ZSH_V<TAB>

你会发现你得到了完整的单词 `${ZSH_VERSION}`，带有右花括号，并且（假设没有其他匹配的参数）后面有一个空格。然而，通常在你以这种方式补全一个参数后，你想在它后面立即输入一些东西，比如一个下标。使用 `AUTO_PARAM_KEYS`，如果你此时输入一些很可能需要放在参数名后面的东西，它会立即被放在那里，而无需你删除中间的字符——例如，用 `[` 试试。请注意，这只有在参数名和任何额外部分是由补全添加时才会发生；如果你手动输入所有内容，输入 `[` 不会有这种神奇的效果。

**`COMPLETE_IN_WORD`**

如果设置了这个，补全总是在单词中的光标位置进行。例如，如果你输入了 `Mafile`，回到 `f` 上，然后按 tab，shell 会补全 `Makefile`，而不是它通常的行为，即转到单词的末尾并尝试在那里找到一个补全，即匹配 `Mafile*` 的东西。某些类型的新补全（例如文件名补全）似乎无论选项设置如何都实现了这种行为；其他一些特性（例如下面描述的 `_prefix` 补全器）需要它，所以设置并习惯它是一件好事，除非你真的只需要在单词的末尾补全。

**`ALWAYS_TO_END`**

如果设置了这个，光标在单词被补全后总是移动到单词的末尾，即使补全是在中间进行的。这也发生在菜单补全中。

[]{#l151}

### 6.2.5: 改变补全的显示方式

**`LIST_TYPES`**

这就像 `ls` 的 `-F` 选项；出现在补全列表中的文件会有一个尾随的 `/` 表示目录，`*` 表示当前进程可执行的常规文件，`@` 表示链接，`|` 表示命名管道，`%` 表示字符设备，`#` 表示块设备。这个选项默认是打开的。

请注意，标识符只有在补全系统知道该项应该是文件时才会出现。如果使用通常的文件名补全命令，这是自动的。如果你编写自己的补全函数并想告诉 shell 这些值可能是现有文件以应用 `LIST_TYPES`，`compadd` 内建命令也有一个 `-f` 选项（但如果不存在这样的文件也不会造成伤害）。

**`LIST_PACKED`, `LIST_ROWS_FIRST`**

这些影响补全列表的排列。使用 `LIST_PACKED`，补全列表通过改变列宽来尽可能紧凑，而不是将它们格式化成一个完全规则的网格。使用 `LIST_ROWS_FIRST`，列表顺序被改变，使得相邻的项目沿行出现而不是下行，有点像 `ls` 的 `-x` 选项。

可以使用 `list-packed` 和 `list-rows-first` 样式为特定上下文改变这两者。在这种情况下，样式总是覆盖选项；如果没有找到相应的样式，则使用选项设置。

还要注意后面关于补全组的讨论：可以让不同类型的补全出现在单独的列表中，然后可以使用这些对标签敏感的样式以不同方式格式化它们。

[]{#l152}

## 6.3: 新补全入门

在我详细介绍新补全之前，这里是如何设置它以便你可以尝试一下。如上所述，进行补全的基本对象是 shell 函数。这些都是自动加载的，所以 shell 需要通过 `$fpath` 数组知道在哪里找到它们。如果 shell 安装正确，并且初始化文件中没有任何东西从 `$fpath` 中移除了所需的部分，这应该会自动发生。甚至可能你的系统为你设置了补全（Mandrake Linux 6.1 是第一个开箱即用这样做的系统），在这种情况下，输入 `which compdef`，你应该会看到一个完整的 shell 函数——实际上是那个允许你定义额外补全函数的函数。然后你可以跳过下一段。

如果你想加载补全，在命令行试试这个：

      autoload -U compinit
      compinit

这应该会静默地工作。如果不行，你需要问你的系统管理员补全函数发生了什么，或者自己找到它们，然后将所有需要的目录添加到你的 `$fpath` 中。它们要么都在一个大目录里，要么在一组子目录里，名字是 `AIX`、`BSD`、`Base`、`Debian`、`Redhat`、`Unix`、`X` 和 `Zsh`；在第二种情况下，所有目录都需要在 `$fpath` 中。当这工作时，你可以将相同的行，包括你需要的任何对 `$fpath` 的修改，添加到你的 `.zshrc` 中。

你现在可以看看它是否真的在工作。输入 `cd `，然后 `^D`，你应该会看到一个只有目录的列表，没有常规文件。如果你设置了 `$cdpath`，你可能会看到 `ls` 看不到的目录。正如这所暗示的，补全系统为许多常见（以及一些相当深奥的）命令提供了补全。的确，其想法是，对于大多数用户来说，补全在大多数时候都能正常工作，无需干预。如果你认为它应该工作但没有，那可能是个 bug 或疏忽，你应该报告它。

另一个关于“它就是能工作”主题的例子：

      tar xzf archive.tar.gz ^D

将查看 gzipped tar 归档文件内部——假设是 GNU 版本的 `tar`，其中第一组参数中的 `z` 报告归档文件已用 gzip 压缩——并给你一个可以提取的文件或目录列表。这与普通文件补全的方式非常相似；尽管有差异，你可以在归档文件内补全到任何目录深度。（在这一点上，你应该印象深刻了。）

补全系统知道的不仅仅是命令及其参数，它还理解一些 shell 语法。例如，有一个名为 `$_comps` 的关联数组，它将命令名称存储为键，将补全函数的名称存储为相应的值。试试输入：

      print ${_comps[

然后 `^D`。你可能会收到一条消息，问你是否真的想看所有可能的补全，即 `$_comps` 的键；如果你说 `y`，你会看到一个列表。如果你插入任何那些键，然后关闭花括号，这样你就有了例如 `${_comps[mozilla]}`，然后按回车，你会看到处理该命令的补全函数；在这种情况下（在撰写本文时）是 `_webbrowser`。这是找出哪个函数正在处理特定命令的一种方法。如果没有条目——即 `print ${_comps[mycmd]}` 给你一个空行——那么该命令没有被特殊处理，将只使用为 `-default-` 上下文定义的任何函数，通常是 `_default`。通常这只会尝试补全文件名。如果你愿意，你可以自定义 `_default`。

除了 `-default-`，`_comps` 的一些其他键也看起来像 `-this-`：它们是特殊上下文，是命令参数之外的地方。我们正在使用名为 `-subscript-` 的上下文；你会发现这种情况下的函数叫做 `_subscript`。许多补全函数的名称只是一个下划线后跟命令或上下文名称，减去任何连字符。如果你想尝尝补全函数的样子，试试 `which _subscript`；你很可能会发现里面有很多你还不知道的其他命令。

重要的是要记住，以这种方式找到的函数是执行补全的根源。无论你如何摆弄选项或样式——我将在接下来的几节中谈论的东西——都无法改变这一点；如果你想改变基本的补全，你只需要编写自己的函数。

顺便说一句，你可能有想要混合使用的旧式补全——或者你可能特别不想混合它们，以便你可以确保一切都以新格式工作。默认情况下，新的补全系统会首先尝试找到一个特定的新式补全，如果找不到，它会尝试为所讨论的命令找到一个 `compctl` 定义的补全。如果所有这些都失败了，它会尝试通常的新式默认补全，可能只是文件名补全。请注意，特定的新式补全优先，这是公平的，因为如果你添加了它们，你几乎肯定不想回到旧的形式。然而，如果你永远不想尝试旧式补全，你可以在你的 `.zshrc` 中放入以下咒语：

      zstyle ':completion:*' use-compctl false

目前，这只是黑魔法，但稍后我将更详细地解释“样式”机制，你会看到这与新式补全中关闭东西的正常方式相符。

[]{#l153}

## 6.4: shell 如何找到正确的补全

[]{#l154}

### 6.4.1: 上下文

上面的例子表明，补全系统是高度上下文敏感的，所以了解这些上下文是如何描述的很重要。这个系统是逐渐演变的，但我所说的一切都适用于所有主版本为 4 的 zsh。

我们处于补全的哪个状态，是以一种冒号分隔的路径给出的，从最不具体的部分开始。有一个简单的方法可以找出你所在的上下文：在你想要补全的地方，输入 `^Xh`，它会告诉你。在 `$_comps` 的例子中，你会发现，

      :completion::complete:-subscript-::

加上一个所谓的“标签”和补全函数的列表，我稍后会讲到。完整的形式是：

      :completion:<func>:<completer>:<command>:<argument>:<tag>

其中元素如果未设置可能会丢失，但冒号总是在那里，以便于模式匹配。在 `:completion:` 部分之后，上下文的各个部分的含义如下，这对于整个补全系统是通用的。

***<func>***
:   是调用补全的函数的名称——如果它是从标准补全系统启动的，则为空白，并且只在一些特殊情况下出现，本章第六节会列出。

***<completer>***
:   在这种情况下被称为 `complete`：这指的是补全系统能做的不仅仅是简单的补全；例如，它可以做一种更受控制的展开（如我提到的），拼写纠正，以及补全带拼写错误的单词。我稍后会介绍其他的补全器；`complete` 是最简单的，只做基本的补全。

***<command>***
:   是命令的名称或如上所述的其他类似上下文，这里是 `-subscript-`。

***<argument>***
:   当 `<command>` 是一个真实命令的名称时最有用；它描述了我们在该命令的参数中的位置。你稍后会看到它是如何工作的。许多更简单的补全不使用这个；只有那些具有复杂选项和参数组合的才使用。如果你需要知道，你只需要用 `^Xh` 找出。

***<tag>***
:   描述补全的类型，本质上是一种区分在命令行同一点可以补全的不同事物的方式。

现在看看一个更正常的命令-参数补全的上下文，例如在 `cd` 之后；在这里你会看到上下文 `:completion::complete:cd::`。这里上下文的命令名部分是一个真实的命令。

对于更复杂的东西，试试在 `cvs add` 之后（如果你没有 `cvs` 命令，这没关系）。你会看到一个长而重复的标签列表，用于两种可能的上下文，

      :completion::complete:cvs:argument-rest:
      :completion::complete:cvs-add:argument-rest:

你两者都有的原因是，`add` 不仅是 `cvs` 的一个参数，如第一个上下文所示，它本身也是一个子命令，有自己的参数，这就是第二个上下文的用途。第一个上下文意味着在 `add` 及其参数之后可能还有更多与它们完全独立的子命令——尽管实际上 CVS 不是那样工作的，所以那种形式在这里不会给你任何补全。

在这两者中，`argument-rest` 表明补全正在寻找另一个参数，`rest` 表示它是行尾的参数列表；如果位置很重要（请看 `cvs import` 的例子），上下文会包含 `argument-1`，或者别的什么。`cvs-add` 显示了子命令是如何处理的，通过用连字符而不是冒号分隔，以免混淆上下文的不同部分。

除了命令和子命令的参数，选项的参数是另一个常见的可能性；举个例子，在 `dvips -o` 之后输入 `^Xh`，你会看到上下文 `:completion::complete:dvips:option-o-1:`；这表明你正在补全 `dvips` 的 `-o` 选项的第一个参数（它只接受一个参数），恰好是输出文件的名称。

[]{#l155}

### 6.4.2: 标签

现在讲到与上下文有关的另一件事，标签。让我们回到 `cd` 命令后的 `^Xh` 帮助测试的完整输出：

      tags in context :completion::complete:cd::
        local-directories path-directories  (_alternative _cd)

与到目前为止考虑的上下文不同，它们告诉你补全是*如何*到达它所在的位置的，标签描述了它*可以*在这里补全的东西。在这种情况下，有三个：`directory-stack` 指的是像 `+1` 这样的条目；目录栈是使用 `pushd` 命令定义的目录集，你可以用 `dirs` 命令看到。接下来，`local-directories` 指的是当前工作目录的子目录，而 `path-directories` 指的是通过搜索 `$cdpath` 数组找到的任何目录。系统提供的每个可能的补全都属于这些类别之一。

在括号里，你看到了被调用来生成补全的函数的名称；如果你想改变基本的补全行为，你需要改变或替换这些。调用函数出现在右边，被调用函数出现在左边，所以在这种情况下，函数 `_cd` 是第一个被调用来处理 `cd` 命令参数的函数，符合通常的惯例。一些标准的补全函数已经从这个列表中被过滤掉了——知道它经过了 `_main_complete` 和 `_complete` 对你没有帮助，例如。

也许已经很明显，让系统以不同方式对待不同类型的补全是有用的，但这里有一个例子，它让你预览了“样式”机制，稍后会讨论。样式是一种美化的 shell 参数；它们用 `zstyle` 命令定义，使用一个样式名和可能的值，这些值可以是一个数组；你总是可以把一个样式定义为一个数组，但一些样式可能只是把它当作一个字符串，用空格连接你给它的参数。你也可以用 `zstyle` 命令，用不同的参数，来检索它们的值，这正是补全系统自己做的；与参数及其值没有实际的重叠，所以它们不会妨碍正常的 shell 编程。

样式与参数的不同之处在于，它们可以在不同的上下文中取不同的值。`zstyle` 命令的第一个参数给出一个上下文；当你定义一个样式时，这个参数实际上是一个模式，将与当前上下文匹配以查看样式是否适用。找出适用规则的规则是：精确的字符串匹配优先于模式，较长的模式优先于较短的模式。这里是那个例子：

      zstyle ':completion:*:cd:*' tag-order local-directories   path-directories

从上面对上下文的讨论中，当 `cd` 命令的参数被补全时，该模式将匹配。正在设置的样式叫做 `tag-order`，值是 `cd` 中对目录有效的两个标签。

`tag-order` 样式决定了标签被尝试的顺序。上面给出的值意味着首先会补全 `local-directories`；只有当没有可以补全的时候，才会尝试 `path-directories`。你可以输入命令并尝试这个；如果你没有设置 `$cdpath`，你可以赋 `cdpath=(~)`，这将允许 `cd foo` 切换到目录 `~/foo` 并相应地补全目录。转到一个不是 `~` 的目录；`cd` 的补全将只显示你所在位置的子目录，而不是 `~` 的子目录，除非你输入一个字符串，它是 `~` 下目录的前缀，但不是你当前目录的前缀。例如，

      % cdpath=(~)
      % ls -F ~
      foo/    bar/
      % ls -F
      rod/    stick/
      # 没有那个 tag-order zstyle 命令，你会得到...
      % cd ^D
      bar/    foo/    rod/    stick/
      % zstyle ':completion:*:cd:*' tag-order local-directories   path-directories
      # 现在你只得到本地目录，如果有的话...
      % cd ^D
      rod/    stick/

你可以用 `tag-order` 样式做更多的事情：如果你把标签放在同一个词里，通过引用，例如 `"local-directories path-directories"`，那么它们会同时被尝试，在这种情况下，你得到的是默认的效果。事实上，由于要知道每个可能的补全有哪些标签可用工作量太大，当没有合适的 `tag-order` 时，默认是简单地一次性尝试上下文中所有可用的标签；这当然是最初 `cd` 之后补全发生的情况。

即使有 `tag-order` 规范，任何未指定的标签通常也会在最后一起被尝试，所以你实际上可以从原始例子的末尾省略 `path-directories`，效果会是一样的。如果你不希望那样发生，你可以在标签列表中指定一个 `-`，它不被用作标签，而是告诉补全只应该尝试列表中的标签，而不是任何其他可能可用的。此外，如果你不希望显示某个特定的标签，你可以在值中包含 `!tagname`，除了这个之外的所有其他标签都会被包含。例如，你可能已经注意到，在命令位置补全时，你会看到要设置的参数以及命令等：

      Completing external command
      tex             texhash         texi2pdf        text2sf
      texconfig       texi2dvi        texindex        textmode
      texdoc          texi2dvi4a2ps   texlinks        texutil
      texexec         texi2html       texshow         texview
      Completing parameter
      TEXINPUTS                               texinputs

（我还没告诉你如何产生那些描述，或者如何让不同标签的补全分开出现，但我会的——请参阅下面对“format”和“group-name”样式的描述。）如果你设置

      zstyle ':completion:*:-command-:*' tag-order '!parameters'

那么最后两行将从补全中消失。当然，你的补全列表可能看起来与我的完全不同。顺便说一句，关于样式的一个好处是，它们是在补全加载之前还是之后定义都无所谓，因为样式是由 shell 的另一部分存储和检索的。

要排除多个标签名，你需要将名称包含在同一个词中。例如，要同时排除参数和保留字，值将是 `'!parameters reserved-words'`，而*不是* `'!parameters' '!reserved-words'`，后者会尝试一次排除参数的补全，然后再一次排除保留字的补全。此外，标签实际上可以是模式，或者更准确地说，`tag-order` 的参数中的任何单词都可能包含一个模式，然后将尝试与所有有效标签进行匹配，看是否匹配。有时甚至使用 `*` 来匹配所有标签也很有用，如果你正在指定一个标签的特殊形式——也许使用一个标签，如下所述——在同一个词中。请参阅手册，了解所提供函数理解的所有标签名称。

`tag-order` 样式允许你给标签“标签”，这是一种别名，指示补全系统使用一个不同名称的标签。你通过给出标签后跟一个冒号，再跟上标签来安排这个。标签前面也可以有一个连字符，这意味着在查找标签时，原始标签名应该放在前面；这真的只是一个让名称看起来更整洁的方式。结果是，通过使用带有标签名的上下文，而不是标签名，你可以安排特殊的行为。此外，你可以为带标签的标签给出一个替代的描述；这些会与我下面将描述的 `format` 样式一起显示（我个人觉得非常有用）。你把描述放在另一个冒号后面，任何空格都要引用。它看起来会是这样：

      zstyle ':completion:*:aliens:*' tag-order   'frooble:-funny:funny frooble' frooble

这在你为 `aliens` 命令补全时使用，该命令大概有标记为 `frooble` 的补全（如果没有，你很奇怪）。然后补全会首先查找该标签的样式，名称为 `frooble-funny`，如果它使用那些样式找到了补全，它会用 `funny frooble` 的描述列出它们（如果你正在使用 `format`）。否则，它会查找该标签的样式，名称为其通常的名称，并再次尝试补全。大概很明显，如果你对标签的两个标签没有不同的样式，你每次都会得到相同的补全。

与其现在通过给出如何使用标签标签的例子来让你对标签信息过载，我将把这个保留到下面对 `ignored-patterns` 样式的描述中，那是标签的一个简洁用法。事实上，它就是为此发明的；可能还有很多我们还没想到的其他用法。

关于 `tag-order` 的一个重要说明，我可能没有像我应该的那样明确：*它不改变在该补全中实际有效的标签*。仅仅将一个标签名放入列表并不意味着该标签名将被使用；那完全由特定上下文的补全函数决定。`tag-order` 样式只是改变了*有效*的标签被检查的顺序。当你搞不清楚为什么 `tag-order` 不做你想要的事情时，再回来读一遍这段。

请注意，测试模式的规则意味着你总是可以通过 `zstyle "*" style ...` 来指定一个万能的最坏情况，它总是最后被尝试——不仅仅是在补全中，实际上，因为 shell 的其他部分也使用样式机制，并且没有 `:completion:` 在上下文的开头，这个样式定义也会在那里被拾取。

像 `tag-order` 这样的样式是标签单独使用的最重要的情况。在其他情况下，它们可以被添加到上下文的末尾；这对于可以为不同的补全集给出不同结果的样式很有用，特别是决定补全列表如何显示，或者一个补全如何插入到命令行中的样式。标签是最后一个元素，所以后面没有冒号。一个完整的上下文然后看起来像 `:completion::complete:cd::path-directories`。稍后，你会看到一些可以为不同的标签上下文有用地不同的样式。然而，请记住，上下文的标签部分，像其他部分一样，如果补全系统还没有弄清楚它应该是什么，可能会是空的。

[]{#l156}

## 6.5: 使用样式配置补全

你现在知道如何为特定的上下文定义一个样式，使用

      zstyle <context> <style> <value...>

以及一些它有用的情况。在介绍其他样式之前，这里有一些更详细的信息。我已经说过样式可以接受一个数组值，即 `zstyle` 命令末尾的一组值，对应于数组元素，你已经看到了一个例子（`tag-order`），其中这是有用的。然而，许多样式只使用一个值。有一个特别常见的情况，你只是想打开或关闭一个值，即一个布尔值。在这种情况下，你可以使用 `true`、`yes`、`on` 或 `1` 表示开，`false`、`no`、`off` 或 `0` 表示关。你以同样的方式定义所有样式；只有当它们被使用时，才决定它们应该是标量、数组还是布尔值，样式名称也不会被检查是否有效，因为 shell 不知道以后可能会查找哪些样式。同样明显地，这也适用于上下文。

你可以使用 `zstyle` 或 `zstyle -L` 来列出现有的样式（不是单独的，只能是完整的列表）。在第二种情况下，它们以 `zstyle` 命令集的形式输出，这些命令可以重新生成当前定义的样式。这对于 `grep` 也很有用，因为你可以很容易地检查特定样式的所有可能上下文。

使用 `zstyle` 最强大的方式是使用 `-e` 选项。这表示你提供的单词将被评估，就像作为 `eval` 的参数一样。这应该将数组 `$reply` 设置为要使用的单词。所以

      zstyle '*' days 'Monday Tuesday'

和

      zstyle -e '*' days 'reply=(Monday Tuesday)'

是等效的——但意图当然是，在第二种情况下，参数每次都可以返回一个不同的值，以便样式可以变化。它通常会在补全的热潮中被评估，因此会获取所有的编辑参数；所以例如

      zstyle -e ':completion:*' mystyles 'reply=(${NUMERIC:-0})'

如果你为命令输入了一个非零的前缀参数，如[第四章](zshguide04.html#zle)中所述，将使样式返回一个非零整数（可能表示 `true`）。然而，参数可以包含任何 zsh 代码，而不仅仅是一个简单的赋值。记住要引用它，以防止它在 `zstyle` 命令行运行时变成别的东西。

最后，你可以通过

      zstyle -d [ <context-pattern> [ <style> ] ] ...

来删除一个样式或一个样式列表的上下文——注意，虽然第一个参数是一个模式，但在这种情况下，它被精确地处理，所以如果你给出模式 `:completion:*:cd:*`，只有用*完全*那个模式给出的值才会被删除，而不是上下文以 `:completion:` 开头并包含 `:cd:` 的其他值。在删除时，模式和样式是可选的；如果省略，上下文的所有样式，或任何类型的所有样式，都会被删除。补全系统有自己的默认值，但这些是内置的，所以你指定的任何东西都有优先权。

顺便说一句，我在[第四章](zshguide04.html#zle)中顺便提到了，你可以在普通的 zle 小部件（用 `zle -N` 创建的那些）中以完全相同的方式使用样式，但你可能马上就忘了。本章关于定义样式和在你自己的函数中使用它们的所有说明都适用于 zle 函数。唯一的区别是，在这种情况下，上下文的约定是，对于执行小部件 *widget-name*，上下文被设置为 `:zle:*widget-name*`。

本节的其余部分描述了一些有用的样式。如果你想让样式的值在不同的地方不同，就由你来试验上下文，或者如果你不在乎，就用 `*`。

[]{#l157}

### 6.5.1: 指定补全器及其选项

“补全器”是决定正在进行何种补全的幕后函数。你用 `completer` 样式来设置使用哪些补全器，它接受一个按顺序尝试的补全器数组。例如，

      zstyle ':completion:*' completer _complete _correct _approximate

指定首先尝试正常补全（`_complete`），然后是拼写纠正（`_correct`），最后是近似补全（`_approximate`），这本质上是前两者的组合效果，即补全输入的单词但允许拼写错误。所有补全器都设置上下文，所以在 `_complete` 内部你通常会找到 `:completion::complete:...`，在纠正内部是 `:completion::correct:..`，依此类推。

补全器有一个标签功能，有点像上面描述但没有详细说明的标签功能。你可以像这样在列表中放入一个补全器：

      zstyle ':completion:*' completer ... _complete:comp-label ...

它调用补全器 `_complete`，但在样式中查找东西时假装它的名字是 `comp-label`，所以你可以多次尝试带有不同功能的补全器。就像标签一样，你可以像 `_complete:-label` 这样写，普通的名字会被前置以得到名字 `complete-label`——只是一个快捷方式，它没有引入任何新东西。我将推迟一个例子，直到你知道补全器做什么。

这里是现有补全器的更详细描述；它们都是函数，所以你可以简单地复制和修改一个来制作你自己的补全器。

**`_complete`**

这是基本的补全行为，我们到目前为止一直假设是这样。它的主要用途只是检查上下文——这里的意思是我们是在补全一个普通的命令参数还是一个特殊的 `-context-` 地方——并调用适当的补全函数。通过设置参数 `compcontext` 来欺骗它是可能的，它将被用来代替自动生成的那个；如果你为特殊情况编写自己的补全命令，这可能很有用。如果你这样做，你应该使该参数对你的函数是局部的。

**`_approximate`**

这做近似补全：它实际上是作为 `_complete` 补全器的包装器编写的，所以它做所有 `_complete` 做的事情，但它也设置系统以允许带拼写错误的补全。通常，你会想先尝试没有拼写错误的补全，所以这个补全器通常出现在 `completers` 样式的 `_complete` 之后。

主要的控制手段是通过 `max-errors` 样式。你可以将它设置为允许的最大错误数。一个错误被定义为如近似模式匹配手册中所述：一个缺失的字符，如 `rhythm` / `rhytm`，一个额外的字符，如 `rhythm` / `rhythms`，一个不正确的字符，如 `rhythm` / `rhxthm`，或一对转置的字符，如 `rhythm` `rhyhtm`，每个都算作一个错误。近似将首先尝试找到一个带有一个错误的匹配，然后是两个错误，依此类推，直到并包括 `max-errors` 的值；选择错误数最少的匹配集，所以即使你将 `max-errors` 设置得很大，错误数较少的匹配总是会被优先选择。设置一个大的 `max-errors` 的真正问题是它会更慢，并且更有可能产生与你想要的完全不同的匹配——对于打字错误，两三个可能就是你需要的最多。否则，总有 Mavis Beacon。因此：

      % zstyle ':completion:*' max-errors 2
      # 只是为了举例...
      % zstyle ':completion:*' completer _approximate
      % ls
      ashes    sackcloth
      % echo siccl<TAB>
        -> echo sackcloth
      % echo zicc<TAB>
        <Beep.>

因为 `s[i/a]c[k]cloth` 只有两个错误，而 `[z/s][i/a]c[k]cloth` 会是三个，所以不补全。

还有另一种给出最大错误数的方法，使用在 Emacs 模式下用 `ESC-<digit>` 指定的数字前缀，在 vi 命令模式下直接用数字键，或者用 `universal-argument`。要启用这个，你必须在 `max-errors` 的值中包含字符串 `numeric`——因此这实际上可以是一个数组，例如

      zstyle ':completion:*:approximate:*' max-errors 2 numeric

自动允许最多两个错误，但你可以通过给补全命令一个前缀来指定一个更高的最大值。所以继续上面的例子，输入新的 `zstyle` 和：

      % echo zicc<ESC-3><TAB>
        -> echo sackcloth

因为我们允许了三个错误。你可以开始看到允许太多错误的问题：如果你有文件 `zucchini`，那只有一个错误，它会被找到并插入，而 `sackcloth` 甚至都不会被考虑。

请注意，上下文在补全器中立即被检查，所以在这个阶段它只是 `:completion::approximate:::`；没有更详细的上下文信息可用，所以不可能为不同的命令或标签指定不同的 `max-errors`。

作为样式值的最后一种可能性是 `not-numeric`：那意味着如果给出了任何数字前缀，近似将根本不被执行。在最后一个例子中，补全将不得不找到一个以 `zicc` 开头的文件。

其他次要的样式也控制近似。样式 `original`，如果为 true，意味着原始值总是被视为一个可能的补全，即使它不匹配任何东西，即使没有其他东西匹配。补全原始值和更正使用不同的标签，不具想象力地称为 `original` 和 `corrections`，所以你可以用 `tag-order` 样式来组织这个。

因为在这种情况下，补全通常与命令行上已有的内容不匹配，并且很可能彼此不匹配，所以会立即进入菜单补全，供你选择一个补全。你可以通过设置布尔样式 `insert-unambiguous` 来安排如果有一个无歧义的部分可以先插入，则不发生这种情况。

最后两个样式（`original` 和 `insert-unambiguous`）在很早的时候就被查找，当生成更正的上下文被设置时，所以只有到补全器名称的上下文是可用的。补全器名称后面会跟着一个连字符和当前接受的错误数。所以对于尝试一个错误的近似，上下文是 `:completion::approximate-1:::`；如果那失败了，系统需要寻找带两个错误的补全，上下文将是 `:completion::approximate-2:::`，依此类推；对于下一个描述的补全器，纠正和 `correct-1` 等也是如此。

**`_correct`**

这与 `_approximate` 非常相似，只是上下文是 `:completion::correct:*`（或者在生成更正时是 `:completion::correct-<num>:*`，如上所述），并且它不会执行补全，只执行拼写纠正，所以补全器必须在行上单词的末尾添加的额外字符现在算作额外的错误，而不是以普通方式补全：`zicc` 离 `sackcloth` 差得很远，七个错误，但 `ziccloth` 只算三个。`_correct` 补全器的控制方式与 `_approximate` 完全相同。

有一个单独的命令只做纠正而不做其他任何事情，通常绑定到 `^Xc`，所以如果你乐于使用那个，你不需要在补全器列表中包含 `_correct`。如果你确实包含了它，并且你也有 `_approximate`，`_correct` 应该排在前面；`_approximate` 肯定会生成 `_correct` 生成的所有匹配，并且可能更多。像其他单独的补全命令一样，它有自己的上下文，这里以 `:completion:correct-word:` 开头，所以很容易让这个命令的行为与正常的补全器不同。

老用户会记得 shell 中内置了另一种形式的拼写纠正，用 `ESC-$` 或 `ESC-s` 调用。这只纠正文件名，并且不理解任何关于新补全机制的东西；使用它的唯一原因是它可能更快。然而，如果你使用 `CORRECT` 或 `CORRECT_ALL` shell 选项，你将使用旧的文件名纠正机制；目前还无法改变这个。

**`_expand`**

这实际上执行展开，而不是补全；区别在本章开头已经解释过了。如果你使用它，你应该将 tab 绑定到 `complete-word`，而不是 `expand-or-complete`，因为否则在补全机制启动之前就会执行展开。由于展开通常仍然应该在补全之前尝试，这个补全器应该出现在 `completers` 样式的值列表中的 `_complete` 及其亲属之前。

使用这个补全器而不是正常展开的原因是，你可以使用 `:completion:*:expand:*` 上下文中的样式来控制执行哪些展开。这里是相关的样式：

**`glob`**
:   展开 glob 表达式，换句话说，使用通配符进行文件名生成。

**`substitute`**
:   展开包含活动 `$` 或反引号的表达式。

但请记住，在使用此补全器时，你需要

      bindkey '^i' complete-word

因为否则由正常绑定 `expand-or-complete` 运行的内置展开机制将接管。

你也可以控制展开是如何插入的。用于添加展开的标签是 `original`（大概不言自明），`all-expansions`，它指的是添加一个包含所有可能展开的单个字符串（默认值，就像编辑器函数 `expand-word` 一样），和 `expansions`，它指的是逐个添加的结果。通过改变标签被尝试的顺序，如上面对 `tag-order` 样式的描述，你可以决定这如何发生。例如，

      zstyle ':completion:*' completer _expand _complete
      zstyle ':completion::expand:*' tag-order expansions

设置为通过补全执行 glob 展开，展开结果逐个呈现（通常通过菜单补全，因为没有共同的前缀）。将 `expansions` 改为 `all-expansions` 会插入列表，就像正常的展开机制所做的那样，而将其改为 `expansions original` 会保留逐个条目，但也会将原始字符串作为一个可能性呈现。你甚至可以三者都有，即整个列表作为一个单一的字符串成为可能性集合中的一个。

还有一个 `sort` 样式，它决定了生成的展开是否会像补全通常那样被排序，还是保持 shell 从展开中产生的原样（例如，展开一个数组参数会按顺序产生元素）。如果它是 `true`，它们将总是被排序，如果 `false` 或未设置则永远不会，如果它是 `menu`，它们将为 `expansions` 标签排序，但对于 `all-expansions` 标签则不会，后者将是原始顺序的值的单个字符串。

当你只尝试生成 `glob` 展开，而不使用 `substitute` 时，有一个轻微的问题。事实上，不难看出，像 `$PWD/*.c` 这样的表达式如果 `substitute` 不活动，就没有任何意义；它必须活动才能理解这样的表达式。然而，如果没有匹配，这很烦人：你最终会得到一个带有展开的 `$PWD` 的补全，但 `*.c` 仍然附加在末尾，这不是你想要的。如果你主要使用 `_expand` 进行通配，你可能因此想将样式 `subst-globs-only` 设置为 true：如果一个补全只是展开了参数，而通配什么也没做，那么展开被拒绝，行保持不变。

`_expand` 补全器也会使用样式

**`accept-exact`**
:   适用于以 `$` 或 `~` 开头的单词。假设有一个参数 `$foo` 和一个参数 `$foobar`，并且你在行上有 `$foo`。通常补全系统会在此时执行补全。然而，设置了 `accept-exact` 后，`$foo` 将被展开，因为它匹配一个参数。

**`add-space`**
:   意味着在展开后添加一个空格，就像成功的补全一样——尽管目录会得到一个 `/`。为了更精细的控制，它可以被设置为单词 `file`，这意味着只有当展开的单词匹配一个已经存在的文件时才会添加空格（想法是，如果它不存在，你可能想进一步补全）。`true` 和 `file` 都可以与 `subst` 结合使用，这可以防止在展开 `${...}` 或 `$(...)` 形式的替换后添加空格。

**`keep_prefix`**
:   也解决了 `~` 或 `$` 是否应该被展开的问题。如果设置了，前缀将被保留，所以将 `~/f*` 展开为 `~/foo` 不会将 `~` 变成 `/home/pws`。默认值是 `changed`，这是 `false` 和 `true` 之间的折中方案：它的意思是，如果单词没有其他变化，即没有找到其他可能的展开，`~` 或 `$` 将被展开。如果这个样式的效果是展开与未展开的单词相同，那么在 `_expand` 之后的列表中的下一个补全器将被尝试。

**`suffix`**
:   与 `keep_prefix` 类似。所指的“后缀”是在以 `~` 或 `$` 开头的表达式之后，不属于该展开的一部分的东西。如果设置了这个样式，并且存在这样的后缀，则不执行展开。所以，例如，`~pw<TAB>` 可以被展开为 `~pws`，但 `~pw/` 不符合展开的条件；同样 `$fo` 和 `$fo/` 也是。这个样式默认为 `true`——所以如果你想让 `_expand` 总是展开这样的表达式，你需要自己将它设置为 `false`。

获得 `_expand` 补全器提供的那种对展开的控制的一种更简单的方法是使用 `_expand_word` 函数，通常绑定到 `\C-xe`，它做所有上面描述的事情，而不会与其他补全器混淆。在这种情况下，上下文字符串以 `:completion:expand-word:` 开头，所以你可以为这个设置与 `_expand` 补全器不同的样式。

为展开设置不同的优先级是补全器标签的一个好用途，例如

      zstyle ':completion:*' completer _expand:-glob _expand:-subst
      zstyle ':completion:*:expand-glob:*' glob yes
      zstyle ':completion:*:expand-subst:*' substitute yes

是使 `_expand` 尝试 glob 补全，如果失败则进行替换，并将结果作为展开呈现的基本设置。你几乎肯定会想添加细节来帮助这个。

**`_history`**

这会从 shell 的历史中补全单词，换句话说，就是你在前几行输入或补全或展开的所有内容。有三个样式会影响它，`sort` 和 `remove-all-dups`；它们在下面为命令小部件 `_history_complete_word` 描述。那个小部件本质上是作为特殊按键执行这个补全器的工作。

**`_prefix`**

严格来说，这个补全器本身不做补全，因此应该在下面以 `_match` 开头的组中。然而，它*似乎*做补全……让我解释一下。

许多 shell，包括 zsh，都有只补全光标前单词的功能，zsh 补全行话称之为“前缀”。我上面在谈论 `expand-or-complete-prefix` 时解释过这个；当你使用那个而不是正常的补全函数时，最终补全的单词看起来像 `<prefix><completion><suffix>`，其中补全将 `<prefix>` 变成了 `<prefix><completion>`，全程忽略了 `<suffix>`。

`_prefix` 补全器让你可以在正常补全中做到这一点。发生的情况是，补全器按正常顺序从左到右评估，直到找到一个补全。如果到达 `_prefix`，则只对前缀尝试补全。所以如果你的补全器是 `_complete _prefix`，shell 会首先尝试对整个单词，前缀和后缀，进行补全，然后只对前缀进行补全。只使用第一个“真实”的补全器（`_complete`、`_approximate`、`_correct`、`_expand`、`_history`）。

你可以通过在补全器列表中多次包含 `_prefix` 来多次尝试前缀补全；第二次，它会尝试列表中的第二个“真实”补全器；所以如果它们是 `_complete _prefix _correct _prefix`，你会得到先是普通补全，然后是只对前缀的相同操作，然后是普通纠正，然后是只对前缀的相同操作。你可以将任一 `_prefix` 补全器移动到序列中你希望尝试仅前缀版本的位置。

`_prefix` 补全器会重新查找 `completer` 样式。这意味着你可以使用一个非默认的补全器集，只用于 `_prefix`。这里，如手册中所述，是如何强制 `_prefix` 只作为最后手段使用，并且只与正常补全一起使用：

      zstyle ':completion:::::' completer _complete   <other-completers> _prefix
      zstyle ':completion::prefix:::' completer _complete

显示了完整的上下文，只是为了强调形式；像往常一样，如果你不在乎，你可以使用通配符。在这种情况下，你可以*只*使用 `_prefix` 作为补全器，而包含后缀的补全将永远不会被尝试；然而，你必须确保你有 `prefix` 上下文的 `completer` 样式，否则根本不会进行任何补全。

补全器标签技巧在这里再次有用：你可以多次调用 `_prefix`，无论你选择在你的补全器列表中的哪个位置，并强制它每次都在不同的上下文中查找。

      zstyle ':completion:*' completer _complete _prefix:-complete   _approximate _prefix:-approximate
      zstyle ':completion:*:prefix-complete:*' completer _complete
      zstyle ':completion:*:prefix-approximate:*' completer _approximate

这会尝试普通补全，然后是只对前缀的相同操作，然后是近似补全，然后是只对前缀的相同操作。如上一段所述，完全可以省略原始的 `_complete` 和 `_approximate` 补全器，只使用带 `_prefix` 前缀的形式。

关于 `_prefix` 补全器的一个陷阱：你必须确保设置了 `COMPLETE_IN_WORD` 选项。这可能听起来违反直觉：毕竟，`_prefix` 强制补全*不*在单词内部进行。关键是，没有那个选项，补全只在单词的末尾尝试，所以当你在 `<prefix><suffix>` 的中间按 `<TAB>` 时，光标会移动到后缀的末尾，然后补全系统才有机会看到那里有什么，因此整个东西被视为一个前缀，没有后缀。

`_prefix` 还有一个样式：`add-space`。这使得 `_prefix` 在补全前缀时添加一个真实的、活生生的空格，而不是仅仅假装那里有一个，从而将补全的单词与原始后缀分开；否则它只会将结果的单词全部连接在一起，就像 `expand-or-complete-prefix` 通常做的那样。

**`_ignored`**

像 `_prefix` 一样，这是一个混合体，收拾已经被生成的补全的烂摊子。它允许你拥有已经被 `ignored-patterns` 样式拒绝的补全。我将在下面描述那个，但它的效果非常简单：对于给定的上下文，你指定的模式列表会与可能的补全进行匹配，任何匹配的都会从列表中移除。`_ignored` 补全器允许你在你的补全器列表的后面检索那些被移除的补全，以防没有其他东西匹配。

这被 `$fignore` 机制使用——一个通常不被补全的文件的后缀列表——它实际上是建立在 `ignored-patterns` 之上的，所以如果你以 zsh 用户熟悉的方式使用它，即如果没有未被忽略的匹配，则显示被忽略的匹配，你需要在你的补全器列表中有 `_ignored` 补全器。

`_ignored` 的一个稍微烦人的特性是，如果只有一个可能的补全，它会被无条件地插入。这几乎不令人惊讶，但如果你真的不想要那个选择，可能会很烦人。有一个样式 `single-ignored`，你可以设置为 `show`——只显示单个被忽略的匹配，不插入——或者 `menu`——进入菜单补全，这样 TAB 键就会在你由 `_ignored` 产生的补全和你最初输入的内容之间循环。后者提供了一种非常自然的处理被忽略文件的方式；它有点像在说“嗯，我找到了这个，但你可能不喜欢，所以如果你想回到你之前的内容，再按一次 tab”。

我说这像 `_prefix`，的确，你可以用完全相同的方式指定为 `_ignored` 补全器调用的补全器，方法是在上下文 `:completion:*:ignored:*` 中给出 `completer` 样式。这意味着我的描述有点过于简化了：`_ignored` 并不真正使用之前被忽略的补全；相反，当它被调用时，它会生成一个可能性列表，其中由 `ignore-patterns` 匹配的选择——或者内部使用 `$fignore`——没有被忽略。所以它真的应该被称为 `_not_ignored`，但它不是。

**`_match`**

这个和剩下的补全器都是实用工具，当放入补全列表时会影响上面给出的主要补全器，而不是它们自己进行补全。

`_match` 补全器应该出现在 `_complete` *之后*；它是 `GLOB_COMPLETE` 选项的一个更灵活的形式。换句话说，如果 `_complete` 没有成功，它会尝试将行上的单词作为模式，而不仅仅是固定字符串，与可能的补全进行匹配。为了让它像正常补全一样工作，它通常表现得好像在光标位置插入了一个 `*`，即使单词已经包含通配符。

你可以用 `match-original` 样式来控制 `*` 的添加；如果这个未设置，则发生正常行为。如果它被设置为 `only`，`*` 不会被插入，如果它是 `true`，或者实际上是任何其他字符串，它会先尝试不带 `*`，然后再带。例如，考虑在使用 `_match` 补全器的情况下输入 `setopt c*ect<TAB>`。通常这会产生两个可能性，`correct` 和 `correctall`。设置样式后，

      zstyle ':completion::match:*' original only

在按 `TAB` 的地方不会插入 `*`，所以 `correct` 是唯一可能的匹配。

`_match` 补全器以与 `_approximate` 完全相同的方式使用 `insert-unambiguous` 样式。

**`_all_matches`**

这与执行展开而不是补全有类似的效果：所有可能的补全都被插入到命令行上。然而，它使用普通上下文补全的结果来实现这一点。补全系统实现这一点的正常方式是通过影响任何随后被调用的补全器的行为——因此你需要将 `_all_matches` 放在你希望具有此行为的任何补全器之前的补全器列表中。

你不太可能想对每种类型的补全都这样做，所以有两种方法可以限制其效果。首先，有 `avoid-completer` 样式：你可以将其设置为一个*不*插入所有匹配的补全器列表，它们将被正常处理。

然后是 `old-matches` 样式。这强制 `_all_matches` 使用一个现有的匹配列表，如果存在的话，而不是这次会生成的。你可以将样式设置为 `only` 而不是 true；在这种情况下，`_all_matches` 将永远不会应用于这次会生成的补全，它只会使用任何已经存在的补全列表。

如果应用于正常的补全生成，这可能会很麻烦——通常的列表永远不会被生成，因为 `_all_matches` 只会插入上次不存在的列表——所以手册推荐了另外两种使用这个补全器和这个样式的方法。首先，你可以给样式的使用添加一个条件：

      zstyle -e ':completion:*' old-matches 'reply=(${NUMERIC:-false})'

除非有非零的数字参数，否则这返回 false；如果你在补全前在 emacs 模式下输入 `<ESC>1`，或者在 vi 模式下只输入 `1`，它将插入由紧接的前一个补全生成的所有值。

否则，你可以单独绑定 `_all_matches`。这可能更有用；复制手册条目：

      zle -C all-matches complete-word _generic
      bindkey '^Xa' all-matches
      zstyle ':completion:all-matches:*' completer _all_matches
      zstyle ':completion:all-matches:*' old-matches only

在这里，我们基于 `complete-word` 小部件生成了一个新的补全，叫做 `all-matches`——这个名字是任意的，但很方便。我们把它绑定到按键 `^Xa`，并给它两个普通补全看不到的特殊样式。对于 `completer`，我们只设置了 `_all_matches`，对于 `old-matches`，我们设置了 `only`；效果是 `^Xa` 只会插入由上一个补全生成的所有补全，无论那是什么——它不必是普通的上下文补全，它可能是任何补全小部件的结果。

**`_list`**

如果你在补全器列表中有这个（放在开头和任何地方一样好），那么你第一次尝试补全时，你只会得到一个列表；什么也不改变，甚至连一个共同的前缀也不会插入。第二次，补全会像往常一样继续。这就像输入 `^D`，然后是 tab，但只用一个键。这与通常的 `AUTO_LIST` 行为不同，因为它完全不考虑补全是否模糊；你总是在第一次得到列表，并且它总是在第二次以通常的方式进行补全。

`_list` 补全器也使用 `condition` 样式，它的工作方式有点像 `_expand` 补全器的样式：它必须被设置为对应于“true”的值之一，`_list` 的延迟行为才会生效。你可以通过在定义样式时使用 `zstyle` 的 `-e` 选项来测试 `$NUMERIC` 的特定值或任何其他条件。

最后，布尔样式 `word` 也是相关的。如果为 false 或未设置，`_list` 在决定是否已更改并因此应延迟补全直到下一次按键时，会检查整行。如果为 true，它只检查当前单词。请注意，`_list` 对那些补全调用之间发生的事情一无所知；查看命令行是它唯一的资源。

**`_menu`**

这只是在 shell 代码中实现菜单补全；它应该在 `completers` 样式中的“真实”补全生成器之前。它忽略 `MENU_COMPLETION` 选项和其他相关选项，并且正常的菜单补全小部件与它工作得不好。然而，你可以复制它并编写自己的补全器。

**`_oldlist`**

这个补全器在你习惯于使用特殊的补全函数，即标准补全系统之外的命令时最有用。它能够保留一个旧的补全列表，否则这个列表会被新生成的列表替换。这有两个方面。

首先，列表。假设你尝试从 shell 历史中补全一些东西，使用绑定到 `ESC-/` 的命令。例如，我输入了 `echo ma<ESC-/>` 并得到了 `max-errors`。在这一点上，你可能想列出可能的补全。不幸的是，如果你输入 `^D`，它只会列出所有通常的上下文补全——对于 `echo` 命令，它没有被特殊处理，这些只是文件。所以它不起作用。通过将 `_oldlist` 补全器放入 `completers` 样式中，*在* `_complete` 之前，它就起作用了，因为旧的匹配列表被保留下来供 `^D` 使用。

在这种情况下，你可以通过将 `old-list` 样式设置为 `always` 或 `never` 来强制打开或关闭旧列表；通常它会显示当前补全集的列表，如果那个还没有显示的话，否则它会生成标准的列表。你甚至可以将 `old-list` 的值设置为一个补全器列表，这些补全器将总是以这种方式保留它们的列表。

`_oldlist` 有用的另一个地方是在菜单补全中，那里会发生完全相同的问题：如果你从一个特殊的命令生成一个菜单，然后尝试通过按 tab 来循环，补全会寻找正常的上下文匹配。这次有一个解决方法——重复使用特殊的命令键而不是 tab。对于多个键序列，这相当繁琐。同样，`_oldlist` 解决了这个问题，你同样可以用一个样式 `old-menu` 来控制行为，它接受一个布尔值（默认是打开的）。正如奥威尔所说，oldlisters unbellyfeel menucomp。

**排序补全器**

我给出了关于补全器应该按什么顺序出现的各种建议，这可能会令人困惑。因此，这里是一个建议的顺序；只需省略你不想使用的任何补全器：

      _all_matches _list _oldlist _menu _expand _complete _match
        _ignored _correct _approximate _prefix 

其他顺序当然是可能的，甚至可能有用：例如，`_all_matches` 补全器适用于所有不在 `avoid-completer` 样式中列出的后续补全器，所以你可能有充分的理由将它移到列表的更后面。

这是我上面提到的补全器标签的例子，即补全器可以用不同的名称来查找。

      zstyle ':completion:*' completer _approximate:-one   _complete:-extended _approximate:-four
      zstyle ':completion:*:approximate-one:*' max-errors 1
      zstyle ':completion:*:complete-extended:*'   matcher 'r:|[.,_-]=* r:|=*'
      zstyle ':completion:*:approximate-four:*' max-errors 4

这按顺序尝试以下内容。

1.  普通的、无修饰的补全。
2.  带一个错误的近似，由第二个样式给出。
3.  带扩展补全的普通补全，由第三个样式给出。抱歉，在我谈论 `matcher` 样式之前，这将是一个黑匣子；现在，你只能相信我的话，这个样式允许方括号中的字符前面有一个通配符，所以 `a-b` 可以补全为 `able-baker`，等等。

4.  最多带四个错误的近似，由最后一个样式给出。

这里有一个相当虚假的例子。你有一个包含以下内容的目录：

      foobar  fortified-badger  frightfully-barbaric

实际上，它一点也不虚假，因为我刚刚创建了一个。首先试试 `echo foo<TAB>`；不出所料，你得到 `foobar`。现在在 `echo` 之后尝试用 `fo-b<TAB>` 补全：基本补全失败，它到达 `_approximate:-one` 并发现它允许一个错误，所以再次接受补全 `foobar`。现在试试 `fort-ba<TAB>`。这次直到第三个补全才起作用，它有效地允许它匹配 `fort*-ba*<TAB>`，所以你看到 `fortified-badger`（不，我自己也没见过，但你知道，它们是夜行动物）。最后，试试 `fortfully-ba<TAB>`；最后一个条目，允许最多四个错误，体贴地将 `or` 纠正为 `righ`，你得到 `frightfully-barbaric`。好吧，这个例子有点精神错乱，但我想你可以看到这些功能是有用的。如果这让你感觉好一点，我花了四五次尝试才把这些样式搞对。

[]{#l158}

### 6.5.2: 改变列表的格式：分组等。

**`format`**

如果你想知道补全列表中的补全来自哪里，你可以使用这个样式。最基本的用法是为任何补全上下文中的 `descriptions` 标签设置它。它接受一个字符串值，其中应出现 `%d`；这将被替换为正在补全的内容的描述。例如，我使用：

      zstyle ':completion:*:descriptions' format 'Completing %d'

如果我输入 `cd^D`，我会看到一个像这样的列表（直到我定义了 `group-name` 样式）：

      Completing external command
      Completing builtin command
      Completing shell function
      cd                cddbsubmit        cdp               cdrecord
      cdctrl            cdecl             cdparanoia        cdswap
      cdda2wav          cdmatch           cdparanoia-yaf
      cddaslave         cdmatch.newer     cdplay
      cddbslave         cdot              cdplayer_applet

顶部的描述与标签名称相关——通常有一一对应的关系——但形式更易读；要获取标签名称，你需要使用 `^Xh`。毫无疑问，你会看到一些不同的东西，但重点是列出的补全是外部命令（例如 `cdplay`）、内建命令（`cd`）和 shell 函数（`cdmatch`，恰好是旧式补全的遗留物，显示了我多久清理一次我的函数目录）的混合体，知道你拥有什么通常很方便。

你可以在描述中使用一些提示符转义，特别是那些打开或关闭突出显示模式（`%S`、`%s`）、粗体文本（`%B`、`%b`）和下划线文本（`%U`、`%u`）的，以使描述从补全列表中脱颖而出。

你可以为除 `descriptions` 之外的某个标签设置这个，这样定义的格式将只用于该标签的补全。

**`group-name`, `group-order`**

在上面的 `format` 例子中，你可能想知道是否可以让不同类型的补全分开显示，并附上描述。你可以使用*分组*来做到这一点。它们也与标签相关，尽管你可以通过 `group-name` 样式定义组名，所以可以为任何上下文中的补全给出不同的名称。然而，最开始最简单的是将样式的值给一个空字符串，这意味着组名就是标签的名称。换句话说，

      zstyle ':completion:*' group-name ''

为每个标签分配一个不同的组名。稍后，如果你决定希望各种标签具有相同的组名，你可以用更具体的模式来微调这个。如果没有定义组名，使用的组被称为 `-default-`，所以这就是在你发出上面的 `zstyle` 命令之前发生的情况；所有匹配都在那个组里。

分组的原因是：同一组中的匹配项一起显示，不同组中的匹配项分开显示。所以上一个例子中的补全列表，在设置了 `format` 和 `group-name` 样式后，变成：

      Completing external command
      cdctrl            cddbsubmit        cdparanoia        cdrecord
      cdda2wav          cdecl             cdparanoia-yaf
      cddaslave         cdot              cdplay
      cddbslave         cdp               cdplayer_applet
      Completing builtin command
      cd
      Completing shell function
      cdmatch                 cdmatch.newer           cdswap

你可能会觉得这更有帮助，或者更乱，这取决于我无法控制的深层心理因素。

如果（且仅当）你正在使用 `group-name`，你也可以使用 `group-order`。顾名思义，它决定了不同补全组的显示顺序。它有点像 `tag-order`，我在第一次介绍标签时描述过：值只是一组组的名称，按你希望看到的顺序排列。手册中的例子与我刚刚展示的列表相关：

      zstyle ':completion:*:-command-' group-order   builtins functions commands

——记住，当补全命令的名称而不是它们的参数时，会使用 `-command-` 上下文。毫不奇怪，那个列表现在变成：

      Completing builtin command
      cd
      Completing shell function
      cdmatch                 cdmatch.newer           cdswap
      Completing external command
      cdctrl            cddbsubmit        cdparanoia        cdrecord
      cdda2wav          cdecl             cdparanoia-yaf
      cddaslave         cdot              cdplay
      cddbslave         cdp               cdplayer_applet

如果你用 `^Xh` 调查可用的标签，你会看到还有其他的，比如别名，我们没有定义它们的顺序。这些会出现在你定义了顺序的那些之后，并以生成匹配的函数决定的某种顺序出现。

**`tag-order`**

正如我已经说过的，我已经描述过这个了，但为了完整起见，这里再次提及。

**`verbose`, `auto-description`**

这些是 `format` 的亲戚，因为它们向列表中添加有用的消息。如果 `verbose` 为 true，生成匹配的函数可以自行决定显示更多关于它们的信息。最常见的情况是描述选项时；处理大量选项描述的标准函数 `_describe` 会测试 `verbose` 样式，并将打印有关它正在补全的选项的信息。

你也可以设置字符串样式 `auto-description`；它对于选项也很有用，在它们没有特殊描述，但确实有一个紧随其后的单个参数，补全已经知道的情况下。然后，用于详细打印的参数的描述将作为 `%d` 在 `auto-describe` 中可用，所以像手册推荐的 `specify: %d` 这样的东西将记录选项本身。所以如果一个命令接受 `-o <output-file>` 并且参数有描述 `output file`，那么 `-o`，当它作为可能的补全出现时，如果没有自己的描述，将有描述 `specify: output file`。事实上，大多数被标准补全函数识别的选项已经提供了自己的描述，这比大多数人可能需要的要微妙得多。

**`list-colors`**

这用于根据文件类型以不同颜色显示文件匹配列表。它基于 GNU 版本 `ls` 使用的 `$LS_COLORS` 环境变量的语法。你需要一个能够显示颜色的终端，比如彩色 xterm，并且应该确保加载了 `zsh/complist` 库（如果你使用 `menu` 样式设置的菜单选择，或者如果你使用这个样式，它应该会自动加载）。但你可以明确地确保：

      zmodload -i zsh/complist

`-i` 会在模块已经加载时保持安静。要安装一套标准的默认颜色，你可以使用：

      zstyle ':completion:*' list-colors ''

——注意使用了 `default` 标签——因为空字符串将值设置为默认值。

如果那对你还不够好，这里有一些更详细的说明。参数 `$ZLS_COLORS` 是 `zsh/complist` 使用的系统的最低层部分。有一个简单的内置默认值，而将样式设置为空字符串等同于：

      ZLS_COLORS="no=00:fi=00:di=01;34:ln=01;36:  pi=40;33:so=01;35:bd=40;33;01:cd=40;33;01:  ex=01;32:lc=\e[:rm=m:tc=00:sp=00:ma=07:hi=00:du=00

它的格式与 `$LS_COLORS` 基本相同，实际上你可以通过使用 `ls` 附带的 `dircolors` 命令来获得更有用的值集：

      ZLS_COLORS="no=00:fi=00:di=01;34:ln=01;36:  pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:  or=40;31;01:ex=01;32:*.tar=01;31:*.tgz=01;31:  *.arj=01;31:*.taz=01;31:*.lzh=01;31:*.zip=01;31:  *.z=01;31:*.Z=01;31:*.gz=01;31:*.deb=01;31:  *.jpg=01;35:*.gif=01;35:*.bmp=01;35:*.ppm=01;35:  *.tga=01;35:*.xbm=01;35:*.xpm=01;35:*.tif=01;35:  *.mpg=01;37:*.avi=01;37:*.gl=01;37:*.dl=01;37:"

你应该查看 `zsh/complist` 模块的手册以获取详细信息，但请特别注意添加了 `ma` 类型，它指定了菜单选择中当前匹配的显示方式。其默认值是使用突出显示模式——与提示中的序列 `%S` 效果相同，你可以用 `print -P %Sfoo` 显示。

然而，你需要直接定义样式，因为补全总是使用它来设置 `$ZLS_COLORS`；否则它不知道它找到的值是来自用户还是来自某个样式的先前值。那采用这种格式：

      zstyle ':completion:*' list-colors "no=00" "fi=00" ...

你可以使用一个已经定义的 `$LS_COLORS`：

      zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

（它将参数按冒号分割成一个数组）因为 `$LS_COLORS` 对于 `ls` 仍然有用，即使直接设置 `$ZLS_COLORS` 没有价值。这应该意味着 GNU ls 和 zsh 产生看起来相似的列表。

允许一些特殊效果。你可以使用模式来告诉文件名如何匹配：实际上，这是默认行为的一部分，例如 '`*.tar=01;31`' 强制 tar 文件为红色。在这种情况下，你仅限于 `*` 后跟一个字符串。然而，有一种方法可以为任何匹配指定颜色，而不仅仅是文件，以及任何模式：使用 `=<pat>=<col>`。这里有两种让 `kill` 的进程列表中的作业变红的方法。

      zstyle ':completion:*:*:kill:*' list-colors '=%*=01;31'

这使用了刚刚描述的方法；作业以 `%` 开头。

      zstyle ':completion:*:*:kill:*:jobs' list-colors 'no=01;31'

这使用标签，而不是模式，来匹配作业行。它有各种优点。因为你正在使用标签，所以为所有使用作业的命令改变这个要容易得多，而不仅仅是 kill——只需从字符串中去掉 `kill`。用另一种方法那是不切实际的，因为它会匹配太多你不想匹配的其他东西。你也不依赖于使用特定的模式。最后，如果你用 `format` 描述来尝试它，你会看到那也得到了颜色，因为它匹配了正确的标签。注意使用 `no` 来指定这适用于正常匹配；其他用于文件类型的两个字母代码在这里没有用。

然而，对于通用模式形式，你还可以使用一个更特殊的效果。通过在模式内部用 `(#b)` 打开“反向引用”，括号就变得活跃，它们匹配的部分可以被单独着色。你通过扩展颜色列表来做到这一点，每个代码前面都有一个 `=` 符号，额外的元素将用于为括号匹配的内容着色。这里是 `kill` 的另一个例子，它将进程号变成红色，但其余部分保持不变。

      zstyle ':completion:*:*:kill:*:processes' list-colors   '=(#b) #([0-9]#)*=0=01;31'

这些象形文字是扩展的 glob 模式。你应该注意，`EXTENDED_GLOB` 选项在样式内部总是打开的——这是 `#b` 生效所必需的。特别是，`#` 意味着“前一个模式部分重复零次或多次”，带有扩展的 glob 模式；有关完整细节，请参阅 globbing 手册页。

**`ignored-patterns`**

许多 shell，包括 zsh，都有一个参数 `$fignore`，它给出一个后缀列表；以这些后缀结尾的文件名通常不用于补全。一个典型的值是：

      fignore=(.o ~ .dvi)

所以正常的文件补全不会产生对象文件、EMACS 备份文件或 TeX DVI 文件。

`ignored-patterns` 样式是这个的扩展。它接受一个数组值，像 `fignore` 一样，但有各种不同之处。首先，这些值是应该匹配*整个*要补全的值的模式，包括前缀（例如文件名的目录部分）和后缀。其次，它们适用于*所有*补全，而不仅仅是文件，因为你可以使用样式机制来调整它以适用于你想要的任何地方，直到特定的标签。

因此，你可以用以下内容替换上面 `$fignore` 的用法：

      zstyle ':completion:*:files' ignored-patterns '*?.o' '*?~' '*?.dvi'

用于标签 `files` 正在使用的补全上下文。额外的 `?` 是因为 `$fignore` 小心地只应用于真正的后缀，即前面有东西的字符串，而 `?` 强制至少有一个字符存在。

实际上，这与 `$fignore` 不完全相同，因为除了 `files` 之外还有其他文件标签；除了你已经见过的目录标签，还有 `globbed-files` 和 `all-files`。前者用于补全函数指定了模式的情况，例如 `*.dvi` 用于 `dvips` 命令名之后的文件。这些不使用这个样式，因为模式已经被充分指定了。这遵循了旧补全系统中 `$fignore` 的行为。另一个轻微的区别是，正如我上面在讨论 `_ignored` 补全器时所说，你可以选择是否希望在正常补全失败时看到那些被忽略的文件，方法是在补全器列表中有 `_ignored`。

另一个标签，`all-files`，在 `globbed-files` 标签失败时应用，并表示在这种情况下任何旧文件都足够好；你可以用 `tag-order` 样式来安排这如何发生。在这个例子中，

      zstyle ':completion:*:*:dvips:argument*'   tag-order globbed-files all-files

足以说明，如果模式没有产生任何文件，即目录中没有 `*.dvi` 文件，你希望看到所有文件。最后这个漫谈的要点是：由于 `all-files` 标签与 `files` 标签是分开的，在这种情况下，你真的会看到所有文件（除了像往常一样以 `.` 开头的文件）。你可能会发现这很有用，但你可以很容易地让 `all-files` 标签的行为与 `files` 相同：

      zstyle ':completion:*:(all-|)files' ignored-patterns ...